// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

import {IVerifier} from "./interfaces/IVerifier.sol";
import {IUltraVerifier} from "./interfaces/IUltraVerifier.sol";

/// @title CompositeVerifier
/// @notice Wraps circuit-specific verifiers and implements the IVerifier interface
/// @dev Each circuit (deposit, transfer, withdraw) has its own verifier contract
///      generated by Noir/Barretenberg. This contract delegates to them.
contract CompositeVerifier is IVerifier {
    /// @notice The deposit circuit verifier
    address public immutable depositVerifier;

    /// @notice The transfer circuit verifier
    address public immutable transferVerifier;

    /// @notice The withdraw circuit verifier
    address public immutable withdrawVerifier;

    error ZeroAddress();

    /// @notice Deploy the composite verifier with references to individual verifiers
    /// @param _depositVerifier Address of the deposit circuit verifier
    /// @param _transferVerifier Address of the transfer circuit verifier
    /// @param _withdrawVerifier Address of the withdraw circuit verifier
    constructor(address _depositVerifier, address _transferVerifier, address _withdrawVerifier) {
        if (_depositVerifier == address(0) || _transferVerifier == address(0) || _withdrawVerifier == address(0)) {
            revert ZeroAddress();
        }
        depositVerifier = _depositVerifier;
        transferVerifier = _transferVerifier;
        withdrawVerifier = _withdrawVerifier;
    }

    /// @inheritdoc IVerifier
    function verifyDeposit(bytes calldata proof, bytes32[] memory publicInputs) external view override returns (bool) {
        return IUltraVerifier(depositVerifier).verify(proof, publicInputs);
    }

    /// @inheritdoc IVerifier
    function verifyTransfer(bytes calldata proof, bytes32[] calldata publicInputs)
        external
        view
        override
        returns (bool)
    {
        return IUltraVerifier(transferVerifier).verify(proof, publicInputs);
    }

    /// @inheritdoc IVerifier
    function verifyWithdraw(bytes calldata proof, bytes32[] calldata publicInputs)
        external
        view
        override
        returns (bool)
    {
        return IUltraVerifier(withdrawVerifier).verify(proof, publicInputs);
    }
}
